// ----------------------------------
// RSDK Project: Sonic 1/Sonic 2
// Script Description: Fire Shield Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias 27 : TYPE_FIRESHIELD

private alias object.value0 : object.timer
private alias object.value18 : object.currentPlane

// Bubble shield states
// 2 sets of 2
//  -> Init state and Main state
//  -> Passive version and active version
private alias 0 : STATE_INITP
private alias 1 : STATE_MAINP
private alias 2 : STATE_INITA
private alias 3 : STATE_MAINA

// Tables
public table FireShield_animSpriteTableP
	0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 6, 7, 4, 5, 2, 3, 0, 1
end table

public table FireShield_animSpriteTableA
	10, 11, 12, 10, 12, 13, 10, 11, 12, 10, 12, 13, 0
end table


event ObjectMain

	object.drawOrder = -1

	// Erase the object if it shouldn't be here
	if object[-playerCount].state == PlayerObject_Death
		object.type = TypeName[Blank Object]
	end if
	if object[-playerCount].state == PlayerObject_Drown
		object.type = TypeName[Blank Object]
	end if

	switch object.state
	case STATE_INITP
		object.animationTimer = 0
		object.timer = 0
		object.currentPlane = 0
		object.direction = FLIP_NONE
		GetTableValue(object.frame, object.timer, FireShield_animSpriteTableP)
		object.state++
		// Fall through
	case STATE_MAINP
		object.animationTimer++
		if object.animationTimer >= 3
			object.animationTimer = 1
			object.timer++
			object.currentPlane ^= 1
			if object.timer >= 10
				object.direction = FLIP_Y
				if object.timer >= 18
					object.timer = 0
					object.direction = FLIP_NONE
				end if
			end if
			GetTableValue(object.frame, object.timer, FireShield_animSpriteTableP)
		end if
		break

	case STATE_INITA
		object.animationTimer = 0
		object.timer = 0
		object.currentPlane = 0
		GetTableValue(object.frame, object.timer, FireShield_animSpriteTableA)
		object.state++
		// Fall through
	case STATE_MAINA
		object.animationTimer++
		if object.animationTimer >= 3
			object.animationTimer = 1
			object.timer++
			if object.timer >= 12
				object.state = 0
			end if
			GetTableValue(object.frame, object.timer, FireShield_animSpriteTableA)
		end if
		break
	end switch
end event


event ObjectDraw
	if PlayerObject_SuperState != 1
		DrawSpriteFX(object.frame, FX_FLIP, object[-playerCount].xpos, object[-playerCount].ypos)
	end if
end event


event ObjectStartup
	LoadSpriteSheet("Global/Items3.gif")
	SpriteFrame(-16, 9, 37, 15, 131, 52)
	SpriteFrame(-20, -24, 39, 15, 131, 68)
	SpriteFrame(-20, 3, 40, 21, 148, 101)
	SpriteFrame(-23, -24, 47, 23, 189, 101)
	SpriteFrame(-24, -9, 48, 29, 148, 125)
	SpriteFrame(-24, -19, 48, 27, 197, 125)
	SpriteFrame(-24, -15, 48, 28, 1, 132)
	SpriteFrame(-24, -21, 48, 34, 50, 140)
	SpriteFrame(-23, -17, 46, 32, 99, 140)
	SpriteFrame(-24, -22, 48, 42, 1, 161)
	SpriteFrame(-4, -21, 20, 43, 64, 175)
	SpriteFrame(-24, -23, 48, 46, 85, 175)
	SpriteFrame(-32, -19, 56, 38, 135, 175)
	SpriteFrame(-40, -24, 63, 47, 192, 166)
	SpriteFrame(0, 0, 1, 1, 1, 1)
end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event
